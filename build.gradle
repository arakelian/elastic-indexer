// elastic-indexer

plugins {
    id 'java'
    id 'maven'
    id 'signing'
    id 'eclipse'
    id 'idea'
    
    // keep dependencies up-to-date!
    id 'com.github.ben-manes.versions' version '0.28.0'

    // useful for creating immutable java beans
    id 'org.inferred.processors' version '3.3.0'

    // to ensure clean code
    id "net.ltgt.errorprone" version "0.7.1"

    // for deployment to Maven Central
    id "io.codearte.nexus-staging" version "0.21.2"

    // for building fat jar
    id "com.github.johnrengelman.shadow" version "5.2.0"

    // useful utilities
    id "com.arakelian.multitool" version "2.4.0"
    
    // for antlr tasks
    id 'antlr'
}

group   = 'com.arakelian'

wrapper {
    gradleVersion = '6.2.2'
}

sourceSets {
    main {
        java {
            srcDirs = ["src/main/java", "src/parser/java"]
        }
    }
    lexer {
        antlr {
            srcDirs = ['src/lexer/antlr']
        }
    }
}

generateGrammarSource {
    // overwrites anything in src/parser/java!
    outputDirectory = new File(projectDir, 'src/parser/java')
    arguments += ["-visitor","-lib","src/lexer/java/com/arakelian/elastic/search/parser"]
}

generateLexerGrammarSource {
    outputDirectory = new File(projectDir, 'src/lexer/java')
}

generateGrammarSource.dependsOn generateLexerGrammarSource

multitool {
    relocates += [
        'org.apache.commons'          : 'repackaged.com.arakelian.elastic.${prefix}org.apache.commons',
        'com.google.common'           : 'repackaged.com.arakelian.elastic.${prefix}com.google.common',
        'com.google.thirdparty'       : 'repackaged.com.arakelian.elastic.${prefix}com.google.thirdparty',
        'javax.annotation'            : 'repackaged.com.arakelian.elastic.${prefix}javax.annotation',

        // antlr
        'org.stringtemplate'          : 'repackaged.com.arakelian.elastic.${prefix}org.stringtemplate',
        'org.antlr.v4'                : 'repackaged.com.arakelian.elastic.${prefix}org.antlr.v4',
        
        // jsonpath links in minidev and objectweb (see https://github.com/json-path/JsonPath/issues/274)
        'com.jayway.jsonpath'         : 'repackaged.com.arakelian.elastic.${prefix}com.jayway.jsonpath',
        'net.minidev.asm'             : 'repackaged.com.arakelian.elastic.${prefix}net.minidev.asm',
        'net.minidev.json'            : 'repackaged.com.arakelian.elastic.${prefix}net.minidev.json',
        'org.objectweb.asm'           : 'repackaged.com.arakelian.elastic.${prefix}org.objectweb.asm',

        // guava is problematic downstream
        'com.fasterxml.jackson.datatype.guava' : 'repackaged.com.arakelian.elastic.${prefix}com.fasterxml.jackson.datatype.guava'
    ]
    
    errorproneCompilerOptions += [
        "-XepExcludedPaths:.*/com/arakelian/elastic/search/parser/.*"
    ]

    proguardConfiguration {
        keep includedescriptorclasses:true, 'class com.arakelian.** { *; }'
        keep includedescriptorclasses:true, 'class **.fasterxml.jackson.datatype.guava.** { *; }'
        keep includedescriptorclasses:true, 'class **.com.jayway.** { *; }'
        keep includedescriptorclasses:true, 'class **.com.google.common.util.concurrent.TimeLimiter { *; }'
        
        // referenced by jayway but not used
        dontwarn 'org.codehaus.jettison.json.**'
        dontwarn 'com.google.gson.**'
        dontwarn 'org.json.**'
        dontwarn 'org.apache.tapestry5.json.**'
        dontwarn '**.com.jayway.jsonpath.spi.json.JettisonProvider'
        
        dontnote '**.com.fasterxml.jackson.datatype.guava.deser.util.RangeFactory'
        dontnote '**.org.apache.commons.lang3.ObjectUtils'
        dontnote '**.com.google.common.util.concurrent.TimeLimiter'
    }
}

dependencies {
    processor 'org.immutables:value:2.8.3'
    
    // annotations
    compile 'org.immutables:value-annotations:2.8.3'

    // configure errorprone version
    errorprone 'com.google.errorprone:error_prone_core:2.3.4'
    
    compile 'com.arakelian:more-commons:2.4.0'
    compile 'com.arakelian:jackson-utils:3.2.0'
    compile 'com.arakelian:json-filter:3.3.1'
    compile 'com.arakelian:key-value-store:2.5.0'
    compile 'com.arakelian:retry:3.7.0'
    
    // caching
    compile 'com.github.ben-manes.caffeine:caffeine:2.8.0'

    // shadow Guava due to frequent downstream incompatibility issues
    shadow  'com.google.guava:guava:28.2-jre'

    // we use Multimap in ElasticDocConfig; we shadow jackson's guava library
    // so we don't add downstream guava dependency
    shadow  ('com.fasterxml.jackson.datatype:jackson-datatype-guava:2.10.1') {
        exclude module: 'jackson-databind'
        exclude module: 'jackson-core'
    }

    // shadow stuff we use very little of, to reduce dependencies
    shadow  'org.apache.commons:commons-lang3:3.9'
    shadow  'commons-io:commons-io:2.6'
    shadow  ('com.jayway.jsonpath:json-path:2.4.0') { exclude module: 'slf4j-api' }

    // third party
    compile 'com.squareup.retrofit2:retrofit:2.6.2'
    compile 'com.squareup.retrofit2:converter-scalars:2.6.2'
    compile 'com.squareup.retrofit2:converter-jackson:2.6.2'
    compile 'com.squareup.retrofit2:adapter-java8:2.6.2'
    compile 'com.squareup.okhttp3:okhttp:4.2.2'
    compile 'com.squareup.okhttp3:logging-interceptor:4.2.2'
    compile 'com.lmax:disruptor:3.4.2'

    antlr   'org.antlr:antlr4:4.7.2'
    shadow  'org.antlr:antlr4-runtime:4.7.2'
    
    // logging
    testCompile 'org.apache.logging.log4j:log4j-api:2.13.1'
    testCompile 'org.apache.logging.log4j:log4j-core:2.13.1'
    testCompile 'org.apache.logging.log4j:log4j-slf4j-impl:2.13.1'
    testCompile 'org.slf4j:jcl-over-slf4j:1.7.29'
    testCompile 'org.slf4j:jul-to-slf4j:1.7.29'
    compile     'org.slf4j:slf4j-api:1.7.29'

    // for unit testing
    testCompile 'junit:junit:4.13'
    testCompile 'net.javacrumbs.json-unit:json-unit:2.14.0'
    testCompile 'com.arakelian:more-commons:2.4.0:tests'
    testCompile 'com.arakelian:jackson-utils:3.2.0:tests'
    testCompile 'com.arakelian:docker-junit-rule:3.4.0'
    testCompile 'com.arakelian:faker:3.4.0'
    testCompile 'com.squareup.retrofit2:retrofit-mock:2.6.2'
    testCompile 'javax.activation:activation:1.1.1'
}
